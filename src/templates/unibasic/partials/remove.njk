$ifndef remove
$define remove

equ RMV_DELIM_END to 0 ;* end of array
equ RMV_DELIM_RECORD to 1 ;* record mark
equ RMV_DELIM_ATTRIBUTE to 2 ;* attribute mark
equ RMV_DELIM_VALUE to 3 ;* value mark
equ RMV_DELIM_SUBVALUE to 4 ;* subvalue mark
equ RMV_DELIM_TEXT to 5 ;* text mark
equ RMV_DELIM_REMOVE1 to 6 ;* remove character 1
equ RMV_DELIM_REMOVE2 to 7 ;* remove character 2

gosub rmvInitVar
go rmvExit

rmvInitVar:
  * this array stores the characters that correspond to the delimiter character codes
  rmvDelimiters = ''
  rmvDelimiters<RMV_DELIM_RECORD> = seq(@rm)
  rmvDelimiters<RMV_DELIM_ATTRIBUTE> = seq(@am)
  rmvDelimiters<RMV_DELIM_VALUE> = seq(@vm)
  rmvDelimiters<RMV_DELIM_SUBVALUE> = seq(@sm)
  rmvDelimiters<RMV_DELIM_TEXT> = seq(@tm)
  rmvDelimiters<RMV_DELIM_REMOVE1> = seq(@tm) - 2
  rmvDelimiters<RMV_DELIM_REMOVE2> = seq(@tm) - 1

  * specifies the delimiter that should be considered when removing values
  * default is subvalue, all values delimited with text mark or lower will be concatenated
  rmvConcatDelim = RMV_DELIM_SUBVALUE

  rmvDataRec = '' ;* this is where we remove the data from
  rmvDataStr = '' ;* this is the removed data that is passed back
  rmvColPos = 0 ;* starting position to begin removing from
  rmvDelimChar = ''
  *
  return

rmvRemove:
  * the remove command returns every value and its delimiter
  * rmvConcatDelim allows us to ignore lesser delimiters and concatenate their values
  rmvDataStr = ''
  loop
    remove rmvText from rmvDataRec at rmvColPos setting rmvDelimChar
    rmvDataStr:= rmvText
    begin case
      case rmvDelimChar eq ''
        * concatenation will not be performed
        exit
      case rmvDelimChar lt RMV_DELIM_RECORD or rmvDelimChar gt RMV_DELIM_REMOVE2
        * concatenation can be done from record mark to remove2
        exit
      case rmvDelimChar gt rmvConcatDelim
        * concatenate values until we encounter the specified delimiter
        rmvDataStr:= char(rmvDelimiters<rmvDelimChar>)
      case 1
        * encountered specified delimiter (or higher order delimiter)
        exit
    end case
  repeat
  return

rmvExit:

$endif
